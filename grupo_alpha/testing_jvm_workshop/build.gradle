ext {
  springVersion = "4.1.6.RELEASE"
}

subprojects {
  apply plugin:'groovy'
  version = "0.0.1"
  group = "com.makingdevs"
  repositories { mavenCentral() }

  dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.11'
    testCompile 'org.spockframework:spock-spring:1.1-groovy-2.4'
    testCompile "junit:junit:4.9"
    testCompile "org.springframework:spring-test:$springVersion"
    testCompile 'mysql:mysql-connector-java:5.1.28'
  }
}

project(":repositoriesAndServices"){
  dependencies {
    compile "org.springframework:spring-context:$springVersion"
    compile "org.springframework:spring-orm:$springVersion"
    compile "org.springframework:spring-aspects:$springVersion"

    compile "org.hibernate:hibernate-entitymanager:4.3.1.Final"
    def excludesList = [
      'slf4j-api',
      'jcl-over-slf4j',
      'spring-orm',
      'spring-context',
      'spring-aop',
      'spring-tx',
      'spring-beans',
      'spring-core'
    ]
    compile("org.springframework.data:spring-data-jpa:1.4.3.RELEASE"){
      excludesList.each { m -> exclude module:m }
    }
    compile 'org.slf4j:slf4j-log4j12:1.7.6'
    compile 'com.h2database:h2:1.3.175'
  }
}

project(":unit_tests"){
  sourceSets {
    main {
      groovy {
        // this makes the groovy-compiler compile groovy- as well
        // as java-files.
        // Needed, because java is normally compiled before groovy.
        // Since we are using groovy objects from java, we need it
        // the other way round.
        srcDirs = ['src/main/groovy', 'src/main/java']
      }
      java {
        srcDirs = [] // don't compile Java code twice
      }
    }
  }
  dependencies {
    testCompile "org.mockito:mockito-all:1.8.5"
  }
}

project(":integration_tests"){
  dependencies {
    compile project(':repositoriesAndServices')
    testCompile "org.springframework:spring-test:$springVersion"
    testCompile 'info.cukes:cucumber-spring:1.2.2'
    testCompile 'info.cukes:cucumber-junit:1.2.2'
  }
}

project(":functional_tests"){
  dependencies {
    testCompile 'org.seleniumhq.selenium:selenium-firefox-driver:2.46.0'
    testCompile 'org.seleniumhq.selenium:selenium-support:2.46.0'
    testCompile 'info.cukes:cucumber-java:1.2.2'
  }
}

project(":specification_tests"){
  dependencies {
    compile project(':repositoriesAndServices')
    testCompile "org.springframework:spring-test:$springVersion"
    testCompile 'org.spockframework:spock-spring:1.0-groovy-2.4'
  }
}
